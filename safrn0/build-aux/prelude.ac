dnl
dnl For the copyright information for this file, please search up the
dnl directory tree for the first COPYING file.
dnl

[

]m4_include([build-aux/gatbps.ac])[

#
# In some cases, the file timestamps from a distribution archive will
# have been altered, which can cause make to mistakenly try to rebuild
# distributed targets that the user can't generally rebuild because of
# missing developer tools. For example, this can happen if the content
# of a distribution archive is stored in a Git repository, as Git does
# not store timestamps. We can work around this problem by making the
# first run of ./configure give all files the same timestamp.
#
# We use test -s instead of test -f because an empty config.status file
# is sometimes used to indicate a dirty working tree before ./configure
# is ever run. The easy install scripts do this, for example.
#

if test -s config.status; then :
  ]AC_MSG_NOTICE([[not normalizing timestamps]])[
else :
  ]AC_MSG_NOTICE([[normalizing timestamps]])[
  t=`date '+%Y%m%d%H%M.%S'` || exit $?
  # Try -exec + first and fall back to -exec ; if needed. Note that
  # -exec + bubbles its error status up to find nicely, whereas for
  # -exec ; we need to bubble it up ourselves.
  find . -exec touch -t $t '{}' + -name .git -prune || {
    f=`find . '(' -exec touch -t $t '{}' ';' \
                  -o -print ')' -name .git -prune` || exit $?
    case $f in
      ?*)
        ]AC_MSG_ERROR([[error normalizing timestamps]], [[1]])[
      ;;
    esac
  }
fi

]
